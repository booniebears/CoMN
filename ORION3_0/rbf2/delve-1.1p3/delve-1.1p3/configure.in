dnl
dnl --------------------------------------------------------------------
dnl Process this file with autoconf to produce a configure script.
dnl --------------------------------------------------------------------
dnl
AC_REVISION($Id: configure.in,v 1.7.2.5.2.1 1997/11/27 15:42:07 revow Exp $)dnl
AC_INIT(Makefile.in)dnl
dnl
dnl --------------------------------------------------------------------
dnl let configure understand more options.  they allow the caller to
dnl specify library- and include-directory for Tcl more explicitly.
dnl
dnl '--with-tcl=DIR' is a shorthand to
dnl '-with-tcl-include-dir=DIR/include --with-tcl-lib-dir=DIR/lib'
dnl --------------------------------------------------------------------

AC_ARG_WITH(tcl,
	[  --with-tcl=DIR          tcl.h resides in DIR/include, libtcl resides
                          in DIR/lib],
	[TCL_LIB_DIR=$withval/lib; TCL_INCLUDE_DIR=$withval/include],
	[TCL_LIB_DIR=NONE; TCL_INCLUDE_DIR=NONE])
dnl
AC_ARG_WITH(tcl-include-dir,
	[  --with-tcl-include-dir=DIR tcl.h resides in DIR],
	[TCL_INCLUDE_DIR=$withval],
	[])
dnl
AC_ARG_WITH(tcl-lib-dir,
	[  --with-tcl-lib-dir=DIR  libtcl resides in DIR],
	[TCL_LIB_DIR=$withval],
	[])

#--------------------------------------------------------------------
#       See if there was a command-line option for where Tcl is;  if
#       not, search standard place.
#--------------------------------------------------------------------

AC_MSG_CHECKING(for Tcl config file)

TCL_CONFIG_FILE="" ;

if test "x$TCL_LIB_DIR" = xNONE ; then
  places="$exec_prefix/lib $prefix/lib /usr/local/lib /usr/lib"
  for dir in $places; do
    if test -f "$dir/tclConfig.sh"; then
      TCL_CONFIG_FILE="$dir/tclConfig.sh"
      break ;
    fi
  done
else
  if test -f "$TCL_LIB_DIR/tclConfig.sh" ; then
      TCL_CONFIG_FILE="$TCL_LIB_DIR/tclConfig.sh"
  fi
fi

if test -n "$TCL_CONFIG_FILE"; then
	AC_MSG_RESULT($TCL_CONFIG_FILE)
else
	AC_MSG_RESULT(not found)
fi


#--------------------------------------------------------------------
#       Read in configuration information generated by Tcl for shared
#       libraries, and arrange for it to be substituted into our
#       Makefile.
#--------------------------------------------------------------------
echo $TCL_CONFIG_FILE
if test -n "$TCL_CONFIG_FILE"; then
  . $TCL_CONFIG_FILE

  echo $TCL_LD_SEARCH_FLAGS
  LDFLAGS="$LDFLAGS $TCL_LD_SEARCH_FLAGS"
  CC=$TCL_CC
else
  TCL_EXEC_PREFIX="NONE"
  TCL_PREFIX="NONE"
  TCL_LIB_SPEC="NONE"
fi

dnl
dnl --------------------------------------------------------------------
dnl Check for alternative programs. Allow the user to set CC or CFLAGS
dnl with configure '--with-' options.
dnl --------------------------------------------------------------------
dnl
AC_ARG_WITH(cc,     [  --with-cc=CC            set compiler to CC],
	[CC="$with_cc"])
dnl
AC_ARG_WITH(cflags, [  --with-cflags=FLAGS     set compiler flags to FLAGS],
    [CFLAGS="$with_cflags"])
dnl
dnl
AC_PROG_CC
AC_PROG_MAKE_SET
AC_CHECK_PROGS(LATEX,latex2e latex,:)
if test "$LATEX" = ":"; then
     DVIPS=: ; AC_SUBST(DVIPS)
     PSNUP=: ; AC_SUBST(PSNUP)
     AC_WARN "couldn't find LaTeX. no documentation will be generated"
else
     AC_CHECK_PROG(DVIPS,dvips,dvips,:)
     if test "$DVIPS" = ":"; then
          PSNUP=: ; AC_SUBST(PSNUP)
          AC_WARN "couldn't find dvips. no postscript documentation will be generated"
     else
          AC_CHECK_PROG(PSNUP,psnup,psnup,:)
     fi
fi


dnl
dnl --------------------------------------------------------------------
dnl Check for UNIX variants that set C preprocessor variables
dnl --------------------------------------------------------------------
dnl

AC_ISC_POSIX

dnl
dnl --------------------------------------------------------------------
dnl Check for header files
dnl --------------------------------------------------------------------
dnl

AC_HEADER_STDC
AC_CHECK_HEADERS(unistd.h)

dnl
dnl --------------------------------------------------------------------
dnl Check for typedefs
dnl --------------------------------------------------------------------
dnl

AC_TYPE_SIZE_T

dnl
dnl --------------------------------------------------------------------
dnl Check for library functions
dnl --------------------------------------------------------------------
dnl

dnl
dnl --------------------------------------------------------------------
dnl Check for structures
dnl --------------------------------------------------------------------
dnl

dnl
dnl --------------------------------------------------------------------
dnl Check for compiler characteristics
dnl --------------------------------------------------------------------
dnl

AC_C_CONST

dnl
dnl --------------------------------------------------------------------
dnl Check for system services
dnl --------------------------------------------------------------------
dnl

dnl
dnl --------------------------------------------------------------------
dnl Locate tcl.h
dnl --------------------------------------------------------------------
dnl

AC_MSG_CHECKING(for directory with tcl.h)

if test "x$TCL_INCLUDE_DIR" != xNONE ; then
  places="$TCL_INCLUDE_DIR"
elif test "x$TCL_PREFIX" != xNONE ; then
  places="$TCL_PREFIX/include"
else
  places="$prefix/include /usr/include /usr/local/include \
   /usr/unsupported/include /usr/share/include /local/include \
   /local/share/include"
fi

include_dir=NONE ;
for dir in $places ; do
  if test -r $dir/tcl.h; then
    include_dir=$dir
    break
  fi
done

if test "x$include_dir" = xNONE ; then
  AC_MSG_ERROR(not found; use --with-tcl=DIR or --with-tcl-include-dir=DIR )
else
  AC_MSG_RESULT($include_dir)
fi

TCL_INCLUDES="-I$include_dir" ;
AC_SUBST(TCL_INCLUDES)

dnl
dnl --------------------------------------------------------------------
dnl Locate tcl library
dnl --------------------------------------------------------------------
dnl

AC_MSG_CHECKING(for tcl library)
if test "x$TCL_LIB_SPEC" = xNONE ; then
  lib_dir=NONE ;
  if test "x$TCL_LIB_DIR" != xNONE ; then
    places="$TCL_LIB_DIR"
  else
    places="$exec_prefix/lib $prefix/lib /usr/lib /usr/local/lib \
     /usr/unsupported/lib /usr/share/lib /local/lib \
     /local/share/lib"
  fi

  for dir in $places ; do
    for version in 7.6 7.5 7.4 7.3 ""; do
      if test "x$TCL_LIB_SPEC" != xNONE ; then
	break
      fi
      for libsuff in .so ".so.*" .a; do
	if test -f "$dir/libtcl$version$libsuff"; then
	  TCL_LIB_SPEC="-L$dir -ltcl$version"
	  TCL_LIB_RUNTIME_DIR=$dir
	  break
	fi
      done
    done
  done
fi

if test "x$TCL_LIB_SPEC" = xNONE ; then
  AC_MSG_ERROR(not found; use --with-tcl=DIR or --with-tcl-lib-dir=DIR )
else
  TCL_LIB_RUNTIME_DIR=$TCL_PREFIX/lib
  AC_MSG_RESULT($TCL_LIB_SPEC)
fi

AC_SUBST(TCL_LIB_SPEC)
AC_SUBST(TCL_LIB_RUNTIME_DIR)

dnl
dnl --------------------------------------------------------------------
dnl Check for the existence of various libraries.  The order here
dnl is important, so that then end up in the right order in the
dnl command line generated by make.  On some SVR4, -lsocket must
dnl be used with -lnsl, so a special check with both libraries, or
dnl the check just fails since it doesn't link (yea, its strange).
dnl --------------------------------------------------------------------
dnl

tk_both=1
AC_FUNC_CHECK(connect, tk_both=0)
AC_FUNC_CHECK(gethostbyname, tk_both=0)
tk_oldLibs=$LIBS
LIBS="$LIBS -lsocket"
AC_CHECK_LIB(nsl, main, tk_both=0)
LIBS=$tk_oldLibs
if test "$tk_both" = 1; then
    echo "need -lsocket and -lnsl together"
    LIBS="$LIBS -lsocket -lnsl"
else
    AC_FUNC_CHECK(connect, ,
	    AC_CHECK_LIB(socket, main, [LIBS="$LIBS -lsocket"]))
    AC_FUNC_CHECK(gethostbyname, ,
	    AC_CHECK_LIB(nsl, main, [LIBS="$LIBS -lnsl"]))
fi

dnl
dnl --------------------------------------------------------------------
dnl Some versions of Sun OS require -ldl to link anything, even
dnl the small, empty programs generated by configure.  I don't
dnl know why, but we must check for it up front or other configure
dnl tests will fail.
dnl --------------------------------------------------------------------
dnl

AC_CHECK_LIB(dl, dlopen, [LIBS="$LIBS -ldl"])

dnl
dnl --------------------------------------------------------------------
dnl Check for additional libraries the Tcl/Tk does not check for.
dnl --------------------------------------------------------------------
dnl

dnl This is needed for DEC Alphas
AC_CHECK_LIB(dnet_stub, main, [LIBS="$LIBS -ldnet_stub"])

dnl--------------------------------------------------------------------
dnl On a few very rare systems, all of the libm.a stuff is
dnl already in libc.a.  Set compiler flags accordingly.
dnl Also, Linux requires the "ieee" library for math to work
dnl right (and it must appear before "-lm").
dnl--------------------------------------------------------------------

AC_CHECK_FUNC(sin, MATH_LIBS="", MATH_LIBS="-lm")
AC_SUBST(MATH_LIBS)
AC_CHECK_LIB(ieee, main, [MATH_LIBS="-lieee $MATH_LIBS"])

dnl--------------------------------------------------------------------
dnl We need the "lgamma" function which should be in the math library,
dnl but may or may not be in the math header check both. We have to
dnl temporarily add the math libraries to LIBS to get the linking right.
dnl--------------------------------------------------------------------

old_LIBS=$LIBS
found_gamma=no
LIBS="$LIBS $MATH_LIBS"
AC_CHECK_FUNCS(lgamma gamma, [found_gamma=yes ; break],)
LIBS=$old_LIBS
if test "$found_gamma" != yes; then
    AC_MSG_ERROR(couldn't find gamma or lgamma math function)
fi

if test "$ac_cv_func_lgamma" = yes; then
    AC_EGREP_HEADER(lgamma, math.h, AC_DEFINE(HAVE_LGAMMA_DECL))
fi
if test "$ac_cv_func_gamma" = yes; then
    AC_EGREP_HEADER(gamma, math.h, AC_DEFINE(HAVE_GAMMA_DECL))
fi

dnl
dnl --------------------------------------------------------------------
dnl use location of tclsh (wish?) as default for prefix, exec-prefix
dnl --------------------------------------------------------------------
dnl

AC_PREFIX_PROGRAM(tclsh)

dnl
dnl --------------------------------------------------------------------
dnl 9.1 Generate the output Makefiles
dnl --------------------------------------------------------------------
dnl

AC_OUTPUT(Makefile doc/Makefile)

